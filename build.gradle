buildscript {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

ext {
    SPRING_VERSION = '5.0.6.RELEASE'
    MOCKITO_VERSION = '2.18.3'
    JUNIT_VERSION = '4.12'
    HAMCREST_VERSION = '1.3'
}
group = 'com.marcnuri'
version = '0.0.0-SNAPSHOT'
sourceCompatibility = 1.8
description = "Common Spring Framework testing utilities"

repositories {
    mavenCentral()
}

dependencies {
    compileOnly group: 'org.springframework', name: 'spring-beans', version: SPRING_VERSION
    compileOnly group: 'org.springframework', name: 'spring-context', version: SPRING_VERSION
    compileOnly group:  'org.mockito', name: 'mockito-core', version: MOCKITO_VERSION
    testCompileOnly group: 'org.springframework', name: 'spring-beans', version: SPRING_VERSION
    testCompileOnly group: 'org.springframework', name: 'spring-context', version: SPRING_VERSION
    testCompileOnly group:  'org.mockito', name: 'mockito-core', version: MOCKITO_VERSION
    testCompileOnly group: 'junit', name: 'junit', version: JUNIT_VERSION
    testCompileOnly group: 'org.hamcrest', name: 'hamcrest-all', version: HAMCREST_VERSION
    testCompileOnly group: 'org.springframework', name: 'spring-test', version: SPRING_VERSION

}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(
                        userName: ossrhUsername,
                        password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(
                        userName: ossrhUsername,
                        password: ossrhPassword)
            }

            pom.project {
                name 'spring-common-test'
                packaging 'jar'
                description 'Common Spring Framework testing utilities'
                url 'https://github.com/manusa/spring-common-test'

                scm {
                    connection 'scm:git:git://github.com/manusa/spring-common-test.git'
                    developerConnection 'scm:git:ssh://github.com:manusa/spring-common-test.git'
                    url 'https://github.com/manusa/spring-common-test'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name 'Marc Nuri'
                        email 'marc@marcnuri.com'
                    }
                }
            }
        }
    }
}